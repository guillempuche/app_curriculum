plugins {
    id "com.android.application"
    id "kotlin-android"
    
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"

    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace = "com.guillempuche.guillem_curriculum"
    compileSdk = 34
    ndkVersion = "26.1.10909125" // You need to install this NDK version in Android Studio's SDK Manager

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId = "com.guillempuche.guillem_curriculum"
        minSdkVersion = 26
        targetSdkVersion = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        // vectorDrawables.useSupportLibrary = true // Support SVG files (used for the app icon)
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAliasRelease']
            keyPassword keystoreProperties['keyPasswordRelease']
            storeFile keystoreProperties['storeFileRelease'] ? file(keystoreProperties['storeFileRelease']) : null
            storePassword keystoreProperties['storePasswordRelease']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            // Add symbols for debugging crashes and ANR https://developer.android.com/build/shrink-code#native-crash-support
            ndk {
                debugSymbolLevel 'FULL'
            }

            // Add this to disable Crashlytics mapping file upload in this build
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'
}